/* REXX */
/**********************************************************************/
/* Replace ADCD default credentials to random, vanilla-RACF passwords */
/*                                                                    */
/*  USERID              PASSWORD        COMMENT                       */
/*  ----------------    ------------ --------------                   */
/*  ADCDMST             - ADCDMST      FULL AUTHORITY                 */
/*  ADCDA THRU ADCDZ    - TEST         LIMITED AUTHORITY(NO OMVS)     */
/*  OPEN1 THRU OPEN3    - SYS1         UID(0) (NO TSO)                */
/*                                                                    */
/**********************************************************************/
call LoadCONSTANTS

/* Iterate through the users and assign a password */

/* New password for ADCDMST */
call AssignPassword ADCDMST GenPassword()

/* New password for ADCDA to ADCDZ */
do letter = 1 to 26
  call AssignPassword ADCD||PASSPIECES.letter GenPassword()
end

/* New password for OPEN1 to OPEN3 */
do count = 1 to 3
  call AssignPassword OPEN||count GenPassword()
end

/* All done */
exit

AssignPassword:
  parse arg user password
  say user password
  ADDRESS TSO "ALTUSER "||user||" PASSWORD("||password||")"
return

GenPassword:
  password = ''
  do until (length(password) > 7)
    i = random(1,PASSPIECES.0)
    newchar = PASSPIECES.i
    password = password||newchar
  end
  return password

LoadCONSTANTS:
  PASSPIECES.0 = 39
  PASSPIECES.1 = A
  PASSPIECES.2 = B
  PASSPIECES.3 = C
  PASSPIECES.4 = D
  PASSPIECES.5 = E
  PASSPIECES.6 = F
  PASSPIECES.7 = G
  PASSPIECES.8 = H
  PASSPIECES.9 = I
  PASSPIECES.10 = J
  PASSPIECES.11 = K
  PASSPIECES.12 = L
  PASSPIECES.13 = M
  PASSPIECES.14 = N
  PASSPIECES.15 = O
  PASSPIECES.16 = P
  PASSPIECES.17 = Q
  PASSPIECES.18 = R
  PASSPIECES.19 = S
  PASSPIECES.20 = T
  PASSPIECES.21 = U
  PASSPIECES.22 = V
  PASSPIECES.23 = W
  PASSPIECES.24 = X
  PASSPIECES.25 = Y
  PASSPIECES.26 = Z
  PASSPIECES.27 = 0
  PASSPIECES.28 = 1
  PASSPIECES.29 = 2
  PASSPIECES.30 = 3
  PASSPIECES.31 = 4
  PASSPIECES.32 = 5
  PASSPIECES.33 = 6
  PASSPIECES.34 = 7
  PASSPIECES.35 = 8
  PASSPIECES.36 = 9
  PASSPIECES.37 = @
  PASSPIECES.38 = #
  PASSPIECES.39 = $
return
